buildscript {

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project.ext.kotlinVersion"
    }
}


apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'


bintray {
    user = JCENTER_USERNAME
    key = JCENTER_API_KEY
    publications = ['ActionsMavenPublication']
    pkg {
        repo = 'maven'
        name = 'com.tmsdurham.actions'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/TicketmasterMobileStudio/actions-on-google-kotlin'
        version {
            name = project.ext.sdkVersion + '-alpha'
            desc = 'Actions on Google SDK - alpha'
            vcsTag = project.ext.sdkVersion
            released = new Date()
        }
    }

}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Patrick Jackson"
            email "patrick.jackson@ticketmaster.com"
        }
    }

    scm {
        url "https://github.com/TicketmasterMobileStudio/actions-on-google-kotlin"
    }
}


publishing {
    publications {
        ActionsMavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.tmsdurham.actions'
            artifactId 'actions-on-google'
            version project.ext.sdkVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Unofficial Actions on Google Client Library for Kolin/Java')
                root.appendNode('name', 'Google Actions Kolin/Java SDK')
                root.appendNode('url', 'https://github.com/TicketmasterMobileStudio/actions-on-google-kotlin')
                root.children().last() + pomConfig
            }
        }
    }
}


junitPlatform {
    filters {
        engines {
            include 'spek'//, 'junit-jupiter', 'junit-vintage'
        }
    }
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jre8:$project.ext.kotlinVersion")

    testCompile('org.jetbrains.spek:spek-api:1.1.2',
            "com.winterbe:expekt:0.5.0",
            'com.nhaarman:mockito-kotlin:1.5.0')
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.2'

    testCompile "com.google.code.gson:gson:$project.ext.gsonVersion"
}

repositories {
    mavenCentral()
}